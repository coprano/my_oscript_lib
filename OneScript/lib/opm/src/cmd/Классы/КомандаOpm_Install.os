///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Процедура ОписаниеКоманды(Знач КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("a all", Ложь, "Установить все пакеты, зарегистрированные в хабе");
	КомандаПриложения.Опция("f file", "", "Указать файл из которого нужно установить пакет. Поддерживает указание маски файла для пакетной установки");
	КомандаПриложения.Опция("u url", "", "Указать прямую интернет-ссылку на файл *.ospx из которого нужно установить пакет");
	
	КомандаПриложения.Опция("l local", Ложь, "Установить пакеты в локальный каталог oscript_modules");
	КомандаПриложения.Опция("dev", Ложь, "Признак установки пакетов для разработки");
	КомандаПриложения.Опция("s skip-install-deps", Ложь, "признак пропуска установки зависимых пакетов");
	КомандаПриложения.Опция("skip-create-app", Ложь, "признак отключения создания файла запуска");
	КомандаПриложения.Опция("d dest", "", "Переопределить стандартный каталог в который устанавливаются пакеты (вместо oscript_modules)");
	
	ОпцияЗеркала = КомандаПриложения.Опция("m mirror", "", "Указать имя сервера, с которого необходимо ставить пакеты.
					|    Доступные сервера прописываются в конфигурационном файле opm.cfg, параметр 'СервераПакетов'.")
					.ВОкружении("OPM_HUB_MIRROR")
					.ТПеречисление();

	МенеджерПолучения = Новый МенеджерПолученияПакетов();
	Для Каждого ДоступноеЗеркало Из МенеджерПолучения.ИменаДоступныхСерверов() Цикл
		ОпцияЗеркала.Перечисление(ДоступноеЗеркало, ДоступноеЗеркало, "Сервер '" + ДоступноеЗеркало + "'");
	КонецЦикла;

	КомандаПриложения.Аргумент("PACKAGE", "", "Имя пакета в хабе. Чтобы установить конкретную версию, используйте ИмяПакета@ВерсияПакета")
						.ТМассивСтрок()
						.Обязательный(Ложь);
						
	// КомандаПриложения.Спек = "(-a | --all | -l | --local | -d | --dest )";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	УстановкаВЛокальныйКаталог = КомандаПриложения.ЗначениеОпции("local");
	УстановкаПакетовРазработчика = КомандаПриложения.ЗначениеОпции("dev");
	УстановкаВсехПакетов = КомандаПриложения.ЗначениеОпции("all");
	КаталогУстановки = КомандаПриложения.ЗначениеОпции("dest");
	ФайлПакетаУстановки = КомандаПриложения.ЗначениеОпции("file");
	ИнтернетСсылкаНаПакет = КомандаПриложения.ЗначениеОпции("url");
	МассивПакетовКУстановке = КомандаПриложения.ЗначениеАргумента("PACKAGE");

	НеобходимоУстановитьЗависимости = Не КомандаПриложения.ЗначениеОпции("skip-install-deps");
	СоздаватьФайлыЗапуска = НЕ КомандаПриложения.ЗначениеОпции("skip-create-app");
	ИмяСервера = КомандаПриложения.ЗначениеОпции("mirror");
	
	РежимУстановки = РежимУстановкиПакетов.Глобально;
	Если УстановкаВЛокальныйКаталог Тогда
		РежимУстановки = РежимУстановкиПакетов.Локально;
	КонецЕсли;
	
	ЦелевойКаталог = Неопределено;

	Если Не ПустаяСтрока(КаталогУстановки) Тогда
		ЦелевойКаталог = КаталогУстановки;
	КонецЕсли;
	Лог = Логирование.ПолучитьЛог(ПараметрыПриложенияOpm.ИмяЛогаСистемы());

	Если РежимУстановки = РежимУстановкиПакетов.Локально Тогда
		Лог.Предупреждение("При локальной установке параметр -dest игнорируется");
		ЦелевойКаталог = Неопределено;
	КонецЕсли;

    Лог.Отладка("УстановкаВЛокальныйКаталог: %1", УстановкаВЛокальныйКаталог);
    Лог.Отладка("УстановкаПакетовРазработчика: %1", УстановкаПакетовРазработчика);
    Лог.Отладка("УстановкаВсехПакетов: %1", УстановкаВсехПакетов);
    Лог.Отладка("КаталогУстановки: %1", КаталогУстановки);
    Лог.Отладка("ФайлПакетаУстановки: %1", ФайлПакетаУстановки);
    Лог.Отладка("ИнтернетСсылкаНаПакет: %1", ИнтернетСсылкаНаПакет);
    Лог.Отладка("МассивПакетовКУстановке: %1", МассивПакетовКУстановке.Количество());
	Лог.Отладка("НеобходимоУстановитьЗависимости: %1", НеобходимоУстановитьЗависимости);
    Лог.Отладка("СоздаватьФайлыЗапуска: %1", СоздаватьФайлыЗапуска);

	НастройкаУстановки = РаботаСПакетами.ПолучитьНастройкуУстановки();
	НастройкаУстановки.УстанавливатьЗависимости = НеобходимоУстановитьЗависимости;
	НастройкаУстановки.УстанавливатьЗависимостиРазработчика = УстановкаПакетовРазработчика;
	НастройкаУстановки.СоздаватьФайлыЗапуска = СоздаватьФайлыЗапуска;
	НастройкаУстановки.ИмяСервера = ИмяСервера;

	Если УстановкаВсехПакетов Тогда
		РаботаСПакетами.УстановитьВсеПакетыИзОблака(РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
	ИначеЕсли ПустаяСтрока(ФайлПакетаУстановки) И ПустаяСтрока(ИнтернетСсылкаНаПакет) И МассивПакетовКУстановке.Количество() = 0 Тогда
		РаботаСПакетами.УстановитьПакетыПоОписаниюПакета(РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
	ИначеЕсли НЕ ПустаяСтрока(ФайлПакетаУстановки) Тогда
		
		РазобранныйАдрес = СтрРазделить(ФайлПакетаУстановки, ПолучитьРазделительПути());
		Путь = ".";
		Маска = ФайлПакетаУстановки;
		Если РазобранныйАдрес.Количество() > 1 Тогда // отделим последнюю секцию как имя файла, а остальное опять соберем в строку пути
			
			Маска = РазобранныйАдрес[РазобранныйАдрес.Количество() - 1];
			РазобранныйАдрес.Удалить(РазобранныйАдрес.Количество() - 1);
			Путь = СтрСоединить(РазобранныйАдрес, ПолучитьРазделительПути());
			
		КонецЕсли;
		
		ФайлыПоМаске = НайтиФайлы(Путь, Маска);
		Для Каждого ФайлПакета Из ФайлыПоМаске Цикл
			
			РаботаСПакетами.УстановитьПакетИзФайла(ФайлПакета.ПолноеИмя, РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
			
		КонецЦикла;
		
	ИначеЕсли НЕ ПустаяСтрока(ИнтернетСсылкаНаПакет) Тогда
		
		позСервер = СтрНайти(ИнтернетСсылкаНаПакет, "/", , , 3);
		Сервер = Лев(ИнтернетСсылкаНаПакет, позСервер-1);
		Адрес = Сред(ИнтернетСсылкаНаПакет, позСервер);
		Если Нрег(Лев(Сервер, 5)) = "https" Тогда
			Порт = 443;
		Иначе
			Порт = 80;
		КонецЕсли;
		
		Соединение = Новый HTTPСоединение(Сервер, Порт);
		Запрос = Новый HTTPЗапрос(Адрес);
		Ответ = Соединение.Получить(Запрос);
		ДД = Ответ.ПолучитьТелоКакДвоичныеДанные();
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла("ospx");
		ДД.Записать(ИмяВременногоФайла);
		
		РаботаСПакетами.УстановитьПакетИзФайла(ИмяВременногоФайла, РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
		
		УдалитьФайлы(ИмяВременногоФайла);

	Иначе

		Для каждого ИмяПакета Из МассивПакетовКУстановке Цикл

			Если ЭтоФайлПакета(ИмяПакета) Тогда
				
				РаботаСПакетами.УстановитьПакетИзФайла(ИмяПакета, РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
			
			Иначе

				РаботаСПакетами.УстановитьПакетИзОблака(ИмяПакета, РежимУстановки, ЦелевойКаталог, НастройкаУстановки);
				
			КонецЕсли;
		
		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

Функция ЭтоФайлПакета(Знач ИмяПакета)
	
	Возврат СтрЗаканчиваетсяНа(НРег(ИмяПакета), ".ospx");

КонецФункции
