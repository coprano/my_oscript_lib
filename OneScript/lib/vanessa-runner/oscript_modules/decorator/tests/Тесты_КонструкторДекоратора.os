#Использовать ".."
#Использовать "./fixtures"
#Использовать asserts

&Тест
Процедура ДекораторСоздается() Экспорт
	Декоратор = Новый КонструкторДекоратора(Новый МойКласс).Построить();

	Ожидаем.Что(ОбработкаДекоратора.ИсходныйТип(Декоратор)).Равно(Тип("МойКласс"));
КонецПроцедуры

&Тест
Процедура ФункцииОбъектаНаследуются() Экспорт
	Декоратор = Новый КонструкторДекоратора(Новый МойКласс).Построить();

	Ожидаем.Что(Декоратор.ТестоваяФункция()).Равно(1);
КонецПроцедуры

&Тест
Процедура ПоляОбъектаНаследуются() Экспорт

	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	Декоратор = Новый КонструкторДекоратора(МойКласс).Построить();

	Ожидаем.Что(Декоратор.Поле).Равно(123);
КонецПроцедуры

&Тест
Процедура ПоляОбъектаСинхронизируютсяЯвно() Экспорт
	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	Декоратор = Новый КонструкторДекоратора(МойКласс).Построить();
	
	Ожидаем.Что(Декоратор.Поле).Равно(МойКласс.Поле);
КонецПроцедуры

&Тест
Процедура ПоляОбъектаСинхронизируютсяОбратно() Экспорт
	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	Декоратор = Новый КонструкторДекоратора(МойКласс).Построить();
	Декоратор.ИзменитьЗначениеПоля(100);

	Ожидаем.Что(Декоратор.Поле).Равно(100);
КонецПроцедуры

&Тест
Процедура ФункцияДобавляется() Экспорт
	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	ИмяМетода = "ДобавленнаяФункция";
	ТекстМетода = "Возврат Истина;";

	Декоратор = Новый КонструкторДекоратора(МойКласс)
		.ДобавитьМетод(ИмяМетода, ТекстМетода)
		.Построить();

	Результат = Декоратор.ДобавленнаяФункция();

	Ожидаем.Что(Результат).ЭтоИстина();
КонецПроцедуры

&Тест
Процедура ПерехватчикиДобавленныхФункцийРаботаютКорректно() Экспорт
	
	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	ИмяМетода = "ЗначениеПоля";
	ТекстМетода = "Поле = Поле + 1;";

	Декоратор = Новый КонструкторДекоратора(МойКласс)
		.ДобавитьПубличноеПоле("КэшПоля")
		.ДобавитьПередВызовомМетода(ИмяМетода, "КэшПоля = Поле;")
		.ДобавитьМетод(ИмяМетода, ТекстМетода)
		.ДобавитьПослеВызоваМетода(ИмяМетода, "КэшПоля = КэшПоля - 1;")
		.Построить();

	Декоратор.ЗначениеПоля();

	Ожидаем.Что(Декоратор.Поле).Равно(124);
	Ожидаем.Что(Декоратор.КэшПоля).Равно(122);

КонецПроцедуры

&Тест
Процедура ПерехватчикиДекораторруемыхФункцийРаботаютКорректно() Экспорт
	
	МойКласс = Новый МойКласс;
	МойКласс.Поле = 123;

	Декоратор = Новый КонструкторДекоратора(МойКласс)
		.ДобавитьПубличноеПоле("КэшПоля")
		.ДобавитьПередВызовомМетода("ИзменитьЗначениеПоля", "КэшПоля = Поле;")
		.ДобавитьПослеВызоваМетода("ИзменитьЗначениеПоля", "КэшПоля = КэшПоля - Поле;")
		.Построить();

	Декоратор.ИзменитьЗначениеПоля(20);

	Ожидаем.Что(Декоратор.Поле).Равно(20);
	Ожидаем.Что(Декоратор.КэшПоля).Равно(103);

КонецПроцедуры

&Тест
Процедура ПриватноеПолеДобавляется() Экспорт

	Декоратор = Новый КонструкторДекоратора(Новый МойКласс)
		.ДобавитьПриватноеПоле("ПриватноеПоле1", "ффф")
		.ДобавитьПриватноеПоле("ПриватноеПоле2")
		.Построить();

	Рефлектор = Новый Рефлектор();

	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Декоратор, "ПриватноеПоле1")).Равно("ффф");
	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Декоратор, "ПриватноеПоле2")).ЭтоНеопределено();

	ИсключениеВозникло = Ложь;
	Попытка
		А = Декоратор.ПриватноеПоле1;
	Исключение
		ИсключениеВозникло = Истина;
	КонецПопытки;

	Ожидаем.Что(ИсключениеВозникло, "Получилось обратиться к приватному полю").ЭтоИстина();
КонецПроцедуры

&Тест
Процедура ПубличноеПолеДобавляется() Экспорт

	Декоратор = Новый КонструкторДекоратора(Новый МойКласс)
		.ДобавитьПубличноеПоле("ПриватноеПоле1", "ффф")
		.ДобавитьПубличноеПоле("ПриватноеПоле2")
		.Построить();

	Рефлектор = Новый Рефлектор();

	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Декоратор, "ПриватноеПоле1")).Равно("ффф");
	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Декоратор, "ПриватноеПоле2")).ЭтоНеопределено();

	ИсключениеВозникло = Ложь;
	Попытка
		А = Декоратор.ПриватноеПоле1;
	Исключение
		ИсключениеВозникло = Истина;
	КонецПопытки;

	Ожидаем.Что(ИсключениеВозникло, "Не получилось обратиться к публичному полю").ЭтоЛожь();
КонецПроцедуры

&Тест
Процедура ИмпортДобавляется() Экспорт

	ТекстСценарияДекоратора = Новый КонструкторДекоратора(Новый МойКласс)
		.ДобавитьИмпортПоИмени("asserts")
		.ДобавитьИмпортПоПути(".")
		.ТекстСценария();

	Ожидаем.Что(ТекстСценарияДекоратора)
		.Содержит("#Использовать asserts")
		.Содержит("#Использовать "".""")
		;

КонецПроцедуры

&Тест
Процедура ПользовательскийШагДобавляется() Экспорт

	ТекстСценарияДекоратора = Новый КонструкторДекоратора(Новый МойКласс)
		.ДобавитьШагИнициализации("А = 0;")
		.ДобавитьШагИнициализации("Сообщить(А);")
		.ТекстСценария();

	Ожидаем.Что(ТекстСценарияДекоратора)
		.Содержит("А = 0;
				  |Сообщить(А);")
		;

КонецПроцедуры

&Тест
Процедура ИсходныйТипОпределяетсяРекурсивно() Экспорт

	Декоратор = Новый КонструкторДекоратора(Новый МойКласс()).Построить();
	ДекораторОтДекоратора = Новый КонструкторДекоратора(Декоратор).Построить();

	Ожидаем.Что(ОбработкаДекоратора.ИсходныйТип(ДекораторОтДекоратора)).Равно(Тип("МойКласс"));

КонецПроцедуры

&Тест
Процедура ПереопределениеТекстаСценария() Экспорт

	КонструкторДекоратора = Новый КонструкторДекоратора(Новый МойКласс())
		.ДобавитьПубличноеПоле("МоеПоле");
	ТекстСценария = КонструкторДекоратора.ТекстСценария();
	ТекстСценария = ТекстСценария + Символы.ПС + "МоеПоле = 100;";

	Декоратор = КонструкторДекоратора.Построить(ТекстСценария);

	Ожидаем.Что(Декоратор.МоеПоле).Равно(100);

КонецПроцедуры

&Тест
Процедура ПереопределилсяВозвращаемыйЭтотОбъект() Экспорт

	Декоратор = Новый КонструкторДекоратора(Новый МойКласс).Построить();

	Ожидаем.Что(Декоратор.ВернутьЭтотОбъект()).Равно(Декоратор);

КонецПроцедуры
