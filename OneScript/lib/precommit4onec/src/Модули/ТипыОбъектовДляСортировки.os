///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль-перечисление с типама объектов для сортировки
//
// (с) BIA Technologies, LLC
//
///////////////////////////////////////////////////////////////////////////////

// BSLLS:ExportVariables-off

Перем Конфигурация Экспорт;
Перем ОпределяемыйТип Экспорт;
Перем ОбщийРеквизит Экспорт;
Перем ПланОбмена Экспорт;
Перем ФункциональнаяОпция Экспорт;
Перем Подсистема Экспорт;
Перем ПодпискаНаСобытие Экспорт;
Перем Неизвестный Экспорт;

// BSLLS:ExportVariables-on

// По файлу определеяет тип сорируемого объекта и некоторые дополнительные данные
Функция ПоФайлу(АнализируемыйФайл) Экспорт
	Описание = Новый Структура("Тип, Заимствованный, ЕДТ", Неизвестный, Ложь, Ложь);
	ЭтоЕДТ = ТипыФайлов.ЭтоФайлОписанияКонфигурацииEDT(АнализируемыйФайл);
	Если ЭтоЕДТ Или ТипыФайлов.ЭтоФайлОписанияКонфигурации(АнализируемыйФайл) Тогда
		Описание.Тип = Конфигурация;
		Описание.ЕДТ = ЭтоЕДТ;
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияОпределяемогоТипа(АнализируемыйФайл) Тогда
		Описание.Тип = ОпределяемыйТип;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияОпределяемогоТипаEDT(АнализируемыйФайл);
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияПланаОбмена(АнализируемыйФайл) Тогда
		Описание.Тип = ПланОбмена;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияПланаОбменаEDT(АнализируемыйФайл);
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияФункциональнойОпции(АнализируемыйФайл) Тогда
		Описание.Тип = ФункциональнаяОпция;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияФункциональнойОпцииEDT(АнализируемыйФайл);
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияПодпискиНаСобытие(АнализируемыйФайл) Тогда
		Описание.Тип = ПодпискаНаСобытие;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияПодпискиНаСобытиеEDT(АнализируемыйФайл);
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияПодсистемы(АнализируемыйФайл) Тогда
		Описание.Тип = Подсистема;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияПодсистемыEDT(АнализируемыйФайл);
	ИначеЕсли ТипыФайлов.ЭтоФайлОписанияОбщегоРеквизита(АнализируемыйФайл) Тогда
		Описание.Тип = ОбщийРеквизит;
		Описание.ЕДТ = ТипыФайлов.ЭтоФайлОписанияОбщегоРеквизитаEDT(АнализируемыйФайл);
	КонецЕсли;

	Если Описание.Тип <> Неизвестный Тогда
		Описание.Заимствованный = ТипыФайлов.ЭтоФайлЗаимствованногоОбъектаРасширения(АнализируемыйФайл);
	КонецЕсли;

	Возврат Описание;
КонецФункции

Конфигурация = "Конфигурация";
ОпределяемыйТип = "ОпределяемыйТип";
ОбщийРеквизит = "ОбщийРеквизит";
ПланОбмена = "ПланОбмена";
ФункциональнаяОпция = "ФункциональнаяОпция";
ПодпискаНаСобытие = "ПодпискаНаСобытие";
Подсистема = "Подсистема";
Неизвестный = "";
