///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с набором методов для определения типа файла
//
// (с) BIA Technologies, LLC
//
///////////////////////////////////////////////////////////////////////////////

// ЭтоФайлИсходников
//	Возвращает истину, если файл является файлом исходных кодов
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлИсходников(Файл) Экспорт

	Если СтрСравнить(Файл.Расширение, ".bsl") = 0 Тогда
		МаркерЗащищенногоМодуля = СтрРазделить("255 255 255 127", " ");
		Буфер = ФайловыеОперации.ПрочитатьДвоичныеДанные(Файл.ПолноеИмя, МаркерЗащищенногоМодуля.Количество());

		Сч = 0;
		Для Каждого Байт ИЗ Буфер Цикл
			Если МаркерЗащищенногоМодуля[Сч] <> Строка(Байт) Тогда
				Возврат Истина;
			КонецЕсли;
			
			Сч = Сч + 1;
		КонецЦикла;

		Возврат Ложь;
	Иначе
		Возврат СтрСравнить(Файл.Расширение, ".os") = 0;
	КонецЕсли;
	
КонецФункции

// ЭтоФайлОписанияКонфигурации
//	Возвращает истину, если файл является файлом описания конфигурации
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияКонфигурации(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Имя, "Configuration.xml") = 0;
	
КонецФункции

// ЭтоФайлОписанияКонфигурацииEDT
//	Возвращает истину, если файл является файлом описания конфигурации в формате EDT
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияКонфигурацииEDT(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Имя, "Configuration.mdo") = 0;
	
КонецФункции

// ЭтоФайлОбычнойФормы
//	Возвращает истину, если файл является файлом обычной формы
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОбычнойФормы(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Имя, "Form.bin") = 0 Или СтрСравнить(Файл.Имя, "Form.oform") = 0;
	
КонецФункции // ЭтоФайлОбычнойФормы

// ЭтоФайлОписанияМетаданных
//	Возвращает истину, если файл является описанием метаданных, исключая файлы описания конфигурации.
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияМетаданных(Файл) Экспорт
	
	Возврат ЭтоФайлОписанияМетаданныхКонфигуратора(Файл) Или ЭтоФайлОписанияМетаданныхEDT(Файл);
	
КонецФункции // ЭтоФайлОписанияМетаданных

// ЭтоФайлОписанияМетаданныхКонфигуратора
//	Возвращает истину, если файл является описанием метаданных конфигуратора, исключая Configuration.xml
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияМетаданныхКонфигуратора(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Расширение, ".xml") = 0 И Не ЭтоФайлОписанияКонфигурации(Файл);
	
КонецФункции // ЭтоФайлОписанияМетаданныхКонфигуратора

// ЭтоФайлОписанияМетаданныхEDT
//	Возвращает истину, если файл является описанием метаданных конфигуратора, исключая Configuration.mdo
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияМетаданныхEDT(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Расширение, ".mdo") = 0 И Не ЭтоФайлОписанияКонфигурацииEDT(Файл);
	
КонецФункции // ЭтоФайлОписанияМетаданныхEDT

// ЭтоФайлОписанияФормы
//	Возвращает истину, если файл является файлом описания формы
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияФормы(Файл) Экспорт
	
	Возврат ЭтоФайлОписанияФормыКонфигуратора(Файл) Или ЭтоФайлОписанияФормыEDT(Файл); 
	
КонецФункции

// ЭтоФайлОписанияФормыКонфигуратора
//	Возвращает истину, если файл является файлом описания формы в формате конфигуратора
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияФормыКонфигуратора(Файл) Экспорт

	Возврат СтрСравнить(Файл.Имя, "Form.xml") = 0;

КонецФункции

// ЭтоФайлОписанияФормыEDT
//	Возвращает истину, если файл является файлом описания формы в формате EDT
// Параметры:
//   Файл - Файл - Полный путь к файлу
//
//  Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлОписанияФормыEDT(Файл) Экспорт
	
	Возврат СтрСравнить(Файл.Имя, "Form.form") = 0;
	
КонецФункции

// Возвращает истину, если передан файл базовой формы расширения в формате EDT.
// Параметры:
//	Файл - Файл - Полный путь к файлу
//
// Возвращаемое значение:
//	Булево - Определяет, является ли переданный файл базовой формой.
//
Функция ЭтоФайлБазовойФормы(Файл) Экспорт
	Возврат СтрЗаканчиваетсяНа(Файл.ПолноеИмя, ПутьКФайлуБазовойФормы());
КонецФункции

// Возвращает относительный путь к файлу базовой формы для формы расширения EDT.
//
// Возвращаемое значение:
//	Строка - Относительный путь к файлу базовой формы.
//
Функция ПутьКФайлуБазовойФормы() Экспорт
	Возврат ОбъединитьПути("BaseForm", "Form.form");
КонецФункции

// ЭтоФайлЧастьТеста
//	Возвращает истину, если файл относится к тестовому расширению
// Параметры:
//   Файл - Файл - Полный путь к файлу
//   КаталогРепозитория - Строка - Путь к репозиторию, для более точного определения принадлежности, необязательный
//
// Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоФайлЧастьТеста(Файл, КаталогРепозитория = Неопределено) Экспорт
	
	ПутьКФайлу = НРег(ПолучитьОтносительныйПуть(Файл.Путь, КаталогРепозитория));
	
	Возврат СтрНайти(ПутьКФайлу, СтрШаблон("%1tests%1", ПолучитьРазделительПути())) <> 0;
	
КонецФункции

// ЭтоОбщийМодуль
//	Возвращает истину, если файл является общим модулем
// Параметры:
//   Файл - Файл - Полный путь к файлу
//   КаталогИсходныхФайлов - Строка - Путь к исходникам, для более точного определения принадлежности, необязательный
//
// Возвращаемое значение:
//   Булево - Признак
//
Функция ЭтоОбщийМодуль(Файл, КаталогИсходныхФайлов = Неопределено) Экспорт
	
	Если ПустаяСтрока(Файл.Расширение) Тогда
		
		Возврат Ложь;
		
	КонецЕсли;
	
	ПутьКФайлу = НРег(ПолучитьОтносительныйПуть(Файл.Путь, КаталогИсходныхФайлов));
	
	Возврат СтрСравнить(Файл.Имя, "Module.bsl") = 0
		И ПутьСодержитКаталог(ПутьКФайлу, "commonmodules");
	
КонецФункции

Функция ЭтоФайлОписанияОпределяемогоТипа(Файл) Экспорт
	Возврат ЭтоФайлОписанияОпределяемогоТипаКонфигуратора(Файл) Или  ЭтоФайлОписанияОпределяемогоТипаEDT(Файл);
КонецФункции

Функция ЭтоФайлОписанияОпределяемогоТипаКонфигуратора(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхКонфигуратора(Файл) И ПутьСодержитКаталог(Файл, "definedtypes");
КонецФункции

Функция ЭтоФайлОписанияОпределяемогоТипаEDT(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхEDT(Файл) И ПутьСодержитКаталог(Файл, "definedtypes");
КонецФункции

Функция ЭтоФайлОписанияПланаОбмена(Файл) Экспорт
	Возврат ЭтоФайлОписанияПланаОбменаКонфигуратора(Файл) Или ЭтоФайлОписанияПланаОбменаEDT(Файл);
КонецФункции

Функция ЭтоФайлОписанияПланаОбменаКонфигуратора(Файл) Экспорт
	Возврат СтрСравнить(Файл.Имя, "Content.xml") = 0 И ПутьСодержитКаталог(Файл, "exchangeplans");
КонецФункции

Функция ЭтоФайлОписанияПланаОбменаEDT(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхEDT(Файл) И ПутьСодержитКаталог(Файл, "exchangeplans");
КонецФункции

Функция ЭтоФайлОписанияПодсистемы(Файл) Экспорт
	Возврат ЭтоФайлОписанияПодсистемыКонфигуратора(Файл) Или ЭтоФайлОписанияПодсистемыEDT(Файл);
КонецФункции

Функция ЭтоФайлОписанияПодсистемыКонфигуратора(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхКонфигуратора(Файл) И ПутьСодержитКаталог(Файл, "subsystems");
КонецФункции

Функция ЭтоФайлОписанияПодсистемыEDT(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхEDT(Файл) И ПутьСодержитКаталог(Файл, "subsystems");
КонецФункции

Функция ЭтоФайлОписанияФункциональнойОпции(Файл) Экспорт
	Возврат ЭтоФайлОписанияФункциональнойОпцииКонфигуратора(Файл) Или ЭтоФайлОписанияФункциональнойОпцииEDT(Файл);
КонецФункции

Функция ЭтоФайлОписанияФункциональнойОпцииКонфигуратора(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхКонфигуратора(Файл) И ПутьСодержитКаталог(Файл, "functionaloptions");
КонецФункции

Функция ЭтоФайлОписанияФункциональнойОпцииEDT(Файл) Экспорт
	Возврат ЭтоФайлОписанияМетаданныхEDT(Файл) И ПутьСодержитКаталог(Файл, "functionaloptions");
КонецФункции

Функция ЭтоФайлПравРоли(Файл) Экспорт
	Возврат СтрСравнить(Файл.Имя, "Rights.xml") = 0 ИЛИ СтрСравнить(Файл.Имя, "Rights.rights") = 0;
КонецФункции

Функция ЭтоМодульМенеджера(Файл) Экспорт
	Возврат СтрСравнить(Файл.Имя, "ManagerModule.bsl") = 0;
КонецФункции

Функция ЭтоМодульОбъекта(Файл) Экспорт
	Возврат СтрСравнить(Файл.Имя, "ObjectModule.bsl") = 0;
КонецФункции

Функция ЭтоМодульНабораЗаписей(Файл) Экспорт
	Возврат СтрСравнить(Файл.Имя, "RecordSetModule.bsl") = 0;
КонецФункции

// По косвенным признакам определеяет принадлежность анализируемого файла к заимствованному в расширение
Функция ЭтоФайлЗаимствованногоОбъектаРасширения(Файл) Экспорт
	Результат = Ложь;

	Если НЕ (Файл.Существует() И Файл.ЭтоФайл()) Тогда
		Возврат Результат;
	КонецЕсли;

	Если ЭтоФайлОписанияПланаОбменаКонфигуратора(Файл) Тогда
		// для плана обмена анализируется файл состава, поэтому смотрим на другой тег
		ОбязательныйТегРасширения = ВРег("<ExtensionProperty>");
	Иначе
		ОбязательныйТегРасширения = ВРег("<ObjectBelonging>");
	КонецЕсли;

	ТекстФайла = ВРег(ФайловыеОперации.ПрочитатьТекстФайла(Файл.ПолноеИмя));
	Возврат СтрНайти(ТекстФайла, ОбязательныйТегРасширения) > 0;
КонецФункции

// Читает текст исходного модуля и проверяет наличие директивы &ИзменениеИКонтроль.
Функция ЭтоМодульРасширенияСКонтролемИзменений(Файл) Экспорт
	Результат = Ложь;

	Если НЕ (Файл.Существует() И Файл.ЭтоФайл())Тогда
		Возврат Результат;
	КонецЕсли;

	СтрокаПоиска = ВРег("&ИзменениеИКонтроль");
	ТекстФайла = ВРег(ФайловыеОперации.ПрочитатьТекстФайла(Файл.ПолноеИмя));
	Возврат СтрНайти(ТекстФайла, СтрокаПоиска) > 0;
КонецФункции

Функция ПолучитьОтносительныйПуть(Путь, Надкаталог)
	
	Результат = Путь;
	
	Если Не ЗначениеЗаполнено(Надкаталог) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ДлинаПутиНадкаталога = СтрДлина(Надкаталог);
	Если СтрСравнить(Надкаталог, Лев(Путь, ДлинаПутиНадкаталога)) = 0 Тогда
		
		Результат = Сред(Путь, ДлинаПутиНадкаталога + 1);
		
		Если Не СтрНачинаетсяС(Результат, ПолучитьРазделительПути()) Тогда
			Результат = ПолучитьРазделительПути() + Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПутьСодержитКаталог(Файл, ИмяКаталога)

	Путь = Файл;
	Если ТипЗнч(Файл) = Тип("Файл") Тогда
		Путь = НРег(Файл.Путь);
	КонецЕсли;

	Уровень = СтрШаблон("%1%2%1", ПолучитьРазделительПути(), ИмяКаталога);
	
	Возврат СтрНайти(Путь, Уровень) > 0;

КонецФункции
