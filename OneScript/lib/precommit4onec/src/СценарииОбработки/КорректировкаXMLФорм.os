///////////////////////////////////////////////////////////////////////////////
// 
// Служебный модуль с реализацией сценариев обработки файлов КорректировкаXMLФорм
//
///////////////////////////////////////////////////////////////////////////////

// ИмяСценария
//	Возвращает имя сценария обработки файлов
//
// Возвращаемое значение:
//   Строка   - Имя текущего сценария обработки файлов
//
Функция ИмяСценария() Экспорт
	
	Возврат "КорректировкаXMLФорм";

КонецФункции // ИмяСценария()

// ОбработатьФайл
//	Выполняет обработку файла
//
// Параметры:
//  АнализируемыйФайл		- Файл - Файл из журнала git для анализа
//  КаталогИсходныхФайлов  	- Строка - Каталог расположения исходных файлов относительно каталог репозитория
//  ДополнительныеПараметры - Структура - Набор дополнительных параметров, которые можно использовать 
//  	* Лог  					- Объект - Текущий лог
//  	* ИзмененныеКаталоги	- Массив - Каталоги, которые необходимо добавить в индекс
//		* КаталогРепозитория	- Строка - Адрес каталога репозитория
//		* ФайлыДляПостОбработки	- Массив - Файлы, изменившиеся / образовавшиеся в результате работы сценария
//											и которые необходимо дообработать
//
// Возвращаемое значение:
//   Булево   - Признак выполненной обработки файла
//
Функция ОбработатьФайл(АнализируемыйФайл, КаталогИсходныхФайлов, ДополнительныеПараметры) Экспорт
	
	Лог = ДополнительныеПараметры.Лог;
	НастройкиСценария = ДополнительныеПараметры.Настройки.Получить(ИмяСценария());
	Если НЕ АнализируемыйФайл.Существует() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФорматEDT = ТипыФайлов.ЭтоФайлОписанияФормыEDT(АнализируемыйФайл);
	Если ФорматEDT ИЛИ ТипыФайлов.ЭтоФайлОписанияФормыКонфигуратора(АнализируемыйФайл) Тогда
	
		Лог.Информация("Обработка файла '%1' по сценарию '%2'", АнализируемыйФайл.ПолноеИмя, ИмяСценария());
		
		Если ФорматEDT Тогда
			Если ТипыФайлов.ЭтоФайлБазовойФормы(АнализируемыйФайл) Тогда
				Возврат Истина;
			Иначе
				ФайлБазовойФормы = Новый Файл(ОбъединитьПути(АнализируемыйФайл.Путь, ТипыФайлов.ПутьКФайлуБазовойФормы()));
				Если ФайлБазовойФормы.Существует() И ОбновитьИндексыЭлементовВФорме(ФайлБазовойФормы) Тогда
					ДополнительныеПараметры.ИзмененныеКаталоги.Добавить(ФайлБазовойФормы.ПолноеИмя);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

		Если ОбновитьИндексыЭлементовВФорме(АнализируемыйФайл) Тогда
			ДополнительныеПараметры.ИзмененныеКаталоги.Добавить(АнализируемыйФайл.ПолноеИмя);
		КонецЕсли;

		Возврат Истина;

	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // ОбработатьФайл()

Функция ОбновитьИндексыЭлементовВФорме(Файл)
	ДанныеФормы = Новый ДанныеФормы(Файл);
	Если ДанныеФормы.ЭтоФормаРасширения() Тогда
		ДанныеФормы.ВосстановитьСвязьЭлементовСБазовойФормой();
	КонецЕсли;

	Если ДанныеФормы.ЕстьДублиИдентификаторов() Тогда
		ДанныеФормы.ЗаменитьДублиИдентификаторов();
	КонецЕсли;

	Если ДанныеФормы.ФормаИзменена() Тогда
		ДанныеФормы.Записать(Файл);
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
КонецФункции
