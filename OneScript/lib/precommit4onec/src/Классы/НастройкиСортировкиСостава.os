///////////////////////////////////////////////////////////////////
// Содержит настройки для сортировки конкретного файла
//
// (с) BIA Technologies, LLC	
//
///////////////////////////////////////////////////////////////////

// BSLLS:ExportVariables-off Геттер не создать
Перем Тип Экспорт;        // Тип обрабатываемого файла
// BSLLS:ExportVariables-on
Перем ЕДТ;                // Признак формата ЕДТ содержимого
Перем Заимствованный;     // Признак отношения файла к расширению и заимствованности из основной конфигурации
Перем Сценарии;           // Сценарии обработки файла
Перем Пустой; // Признак отсутствия настроек

// Конструктор создания на основании анализируемого файла
Процедура ПриСозданииОбъекта(АнализируемыйФайл)
	ОписаниеПоФайлу = ТипыОбъектовДляСортировки.ПоФайлу(АнализируемыйФайл);
	Если ОписаниеПоФайлу.Тип = ТипыОбъектовДляСортировки.Неизвестный Тогда
		Пустой = Истина;
		Возврат;
	КонецЕсли;

	Пустой = Ложь;
	Тип = ОписаниеПоФайлу.Тип;
	ЕДТ = ОписаниеПоФайлу.ЕДТ;
	Заимствованный = ОписаниеПоФайлу.Заимствованный;
	Сценарии = Новый Массив;
КонецПроцедуры

Функция Пустой() Экспорт
	Возврат Пустой;
КонецФункции

Функция ЕДТ() Экспорт
	Возврат ЕДТ;
КонецФункции

Функция Заимствованный() Экспорт
	Возврат Заимствованный;
КонецФункции

Процедура ДобавитьСценарий(НастройкиСценария) Экспорт

	// 1-я группа: состав объектов
	ШаблонПоискаСостава = "";
	Если Тип = ТипыОбъектовДляСортировки.Конфигурация И ЕДТ Тогда
		// 2-я группа: завершающий блок ЕДТ
		ШаблонПоискаСостава = "\s*(<languages[\w\W]*)(<\/mdclass\:Configuration>)[ \t]*\n*"; 
	ИначеЕсли НастройкиСценария.ЕстьРодительскийЭлемент() Тогда
		РодительскийЭлемент = НастройкиСценария.РодительскийЭлемент();
	 	ШаблонПоискаСостава = СтрШаблон("\s*<%1>([\w\W]*?)<\/%2>[ \t]*\n*", 
			РодительскийЭлемент.Имя, РодительскийЭлемент.Завершение);
	Иначе
	 	ШаблонПоискаСостава = СтрШаблон("\s*(<%1>[\w\W]*<\/%1>)[ \t]*\n*", НастройкиСценария.ИмяЭлемента());
	КонецЕсли;

	// 1-я группа: имя объекта состава
	ШаблонПоискаЭлементов = "";
	ТекстАтрибутов = ?(НастройкиСценария.САтрибутами(), "\s+[^>]+", "");
	Если НастройкиСценария.ЕстьВложенный() Тогда
		ШаблонПоискаЭлементов = СтрШаблон( // BSLLS:NestedFunctionInParameters-off используются геттеры
			"<%1%2>[\w\W]*?<%3>([^<]*?)<\/%3>[\w\W]*?<\/%1>",
			НастройкиСценария.ИмяЭлемента(), ТекстАтрибутов, НастройкиСценария.ИмяВложенного());
	Иначе
		ШаблонПоискаЭлементов = СтрШаблон("<%1%2>([^<]*?)<\/%1>", НастройкиСценария.ИмяЭлемента(), ТекстАтрибутов);
	КонецЕсли;

	НастройкиСценария.УставитьШаблоныПоиска(ШаблонПоискаСостава, ШаблонПоискаЭлементов);
	Сценарии.Добавить(НастройкиСценария);
КонецПроцедуры

Функция Сценарии() Экспорт
	Возврат Сценарии;
КонецФункции
