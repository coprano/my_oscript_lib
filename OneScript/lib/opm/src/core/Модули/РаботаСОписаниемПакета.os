#Использовать logos

Перем Лог;

Функция ПрочитатьОписаниеПакета() Экспорт

	Описание = Новый ОписаниеПакета();

	ПутьКМанифесту = ОбъединитьПути(ТекущийКаталог(), КонстантыOpm.ИмяФайлаСпецификацииПакета);
	
	Файл_Манифест = Новый Файл(ПутьКМанифесту);
	Если Файл_Манифест.Существует() Тогда
		Контекст = Новый Структура("Описание", Описание);
		ЗагрузитьСценарий(ПутьКМанифесту, Контекст);
	КонецЕсли;

	Возврат Описание;

КонецФункции

Процедура ПроверитьВерсиюМанифеста(Манифест) Экспорт

	Свойства = Манифест.Свойства();
	Если НЕ Свойства.Свойство("ВерсияМанифеста") Тогда
		Возврат;
	КонецЕсли;

	ИмяПакета = Свойства.Имя;
	ТребуемаяВерсияМанифеста = Свойства.ВерсияМанифеста;
	ТекущаяВерсияМанифеста = КонстантыOpm.ВерсияПродукта;
	Лог().Отладка(
		"ПроверитьВерсиюМанифеста: Перед вызовом СравнитьВерсии(ЭтаВерсия = <%1>, БольшеЧемВерсия = <%2>)",
		ТребуемаяВерсияМанифеста,
		ТекущаяВерсияМанифеста
	);
	Если РаботаСВерсиями.СравнитьВерсии(ТребуемаяВерсияМанифеста, ТекущаяВерсияМанифеста) > 0 Тогда
			ТекстСообщения = СтрШаблон(
			"Ошибка установки пакета <%1>: Обнаружена устаревшая версия opm.
			|Требуемая версия: %2
			|Текущая версия: %3
			|Обновите opm перед установкой пакета",
			ИмяПакета,
			ТребуемаяВерсияМанифеста,
			ТекущаяВерсияМанифеста
		);

		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьВерсиюСреды(Манифест) Экспорт

	Свойства = Манифест.Свойства();
	Если НЕ Свойства.Свойство("ВерсияСреды") Тогда
		Возврат;
	КонецЕсли;

	ИмяПакета = Свойства.Имя;
	ТребуемаяВерсияСреды = Свойства.ВерсияСреды;
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияСреды = СистемнаяИнформация.Версия;
	Лог().Отладка("ПроверитьВерсиюСреды: Перед вызовом СравнитьВерсии(ЭтаВерсия = <%1>, БольшеЧемВерсия = <%2>)", ТребуемаяВерсияСреды, ВерсияСреды);
	Если РаботаСВерсиями.СравнитьВерсии(ТребуемаяВерсияСреды, ВерсияСреды) > 0 Тогда
			ТекстСообщения = СтрШаблон(
			"Ошибка установки пакета <%1>: Обнаружена устаревшая версия движка OneScript.
			|Требуемая версия: %2
			|Текущая версия: %3
			|Обновите OneScript перед установкой пакета",
			ИмяПакета,
			ТребуемаяВерсияСреды,
			ВерсияСреды
		);

		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

КонецПроцедуры

Функция Лог()
	Если Лог = Неопределено Тогда
		Лог = Логирование.ПолучитьЛог("oscript.app.opm");
	КонецЕсли;

	Возврат Лог;
КонецФункции
